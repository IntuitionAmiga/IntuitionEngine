10 REM === ROTOZOOMER WITH SID MUSIC ===
20 REM Enable VideoChip 640x480
30 POKE &HF0000, 1: POKE &HF0004, 0
40 REM === LOAD SID FILE INTO MEMORY ===
50 SA=&H710000
60 BLOAD "Yummy_Pizza.sid", SA
70 SID PLAY SA, 3725
75 PRINT "SID STATUS=";SID STATUS
76 POKE8 &HF0E18, 15
80 REM === GENERATE TEXTURE (256x256 checkerboard at 0x500000) ===
90 TB=&H500000: ST=1024
100 W=&HFFFFFFFF: B=&HFF000000
110 BLIT FILL TB, 128, 128, W, ST
120 BLIT FILL TB+512, 128, 128, B, ST
130 BLIT FILL TB+131072, 128, 128, B, ST
140 BLIT FILL TB+131584, 128, 128, W, ST
150 BB=&H600000: FP=65536
200 REM === MAIN LOOP ===
210 A=0: SI=0
300 REM === FRAME ===
310 SC=0.5+SIN(SI)*0.3: IF SC<0.2 THEN SC=0.2
320 CA=COS(A)/SC: SA=SIN(A)/SC
330 REM Per-pixel deltas in 16.16 fixed-point
340 DC=INT(CA*FP): DS=INT(SA*FP)
350 REM Starting UV for pixel (0,0), centered on texture
360 CX=128: CY=128: HW=320: HH=240
370 SU=INT((CX-HW*CA+HH*SA)*FP)
380 SV=INT((CY-HW*SA-HH*CA)*FP)
400 BLIT MODE7 TB, BB, 640, 480, SU, SV, DC, DS, 0-DS, DC, 255, 255, ST, 2560
600 REM === FLIP AND ANIMATE ===
610 BLIT MEMCOPY &H600000, &H100000, 307200
615 VSYNC
620 A=A+0.03: IF A>6.28318 THEN A=A-6.28318
630 SI=SI+0.01
640 GOTO 300
